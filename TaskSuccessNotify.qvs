////////////////////////////////////////////////////////////////////
//	Author - Jo√£o de Souza
//	http://datalakers.com.br
//	30/01/2019
//
//  Based on the code Task Error Notify
//	Author - Aaron Couron
//	http://livingqlik.com
//	10/24/2018
//
//	the author grants free use of this app but claims no liability
//	or responsibility for any negative effects you might
//	encounter using it within any Qlik Sense environment.
///////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////
//	PREREQUISITES:
//	This app requires the (free) SMTP Web Connector within the
//		Qlik QlikWebConnectors package found at the Qlik software download page.
//	This app utilizes the ServerLogsFolder which will require developer to have access
//		to this built in connection.
//	The Qlik Server will need to be put into Legacy mode because the data sources
//		require dynamic URLs.
///////////////////////////////////////////////////////////////////


///////////////////////////////////////////////////////////////////
//	OBS:
//	I dont put the Qlik Server into Legacy mode.
//  There's need to create a Connetor "Web Archive" with the url
// to the WebConnector "http://localhost:5555/"
//
///////////////////////////////////////////////////////////////////



//	Set Log Location
set vFileLocation	=	'lib://LogSchedulerAudit (win-qp4blc317f1_administrator)';

//	Set here how many minutes ago you want to scoop logs for
//	This should coincide with the frequency in minutes of the reload task for this app.
set vMinutesToGoBack	=	2;
let vServerName = ComputerName();

//	email settings:
set vRecipient	=	'joao.souza@datalakers.com.br';
set vQwcConnectionName = 'lib://SMTP Connector (win-qp4blc317f1_administrator)';
set vMailServer	=	'smtp.gmail.com';
set vPort	=	587;
set vSSLMode	=	'Explicit';
set vSubject	=	'$(vServerName) | Task finalizada com sucesso!';
// set vMessage	= 	'Reload task errors have been encountered within the last $(vMinutesToGoBack) minutes on the Qlik Sense Server. Log in to the <a href="https://con-qssrv.calprocorp.local/qmc/tasks">QMC</a> to review the tasks and associated log entries.';
set vFromName	=	'Task Success Monitor';
set	vFromEmail	=	'taskerrormonitor@gmail.com';
set vUserName	=	'taskerrormonitor@gmail.com';



///////////////////////////////////////////////////////////////
//	Do not adjust past this line
///////////////////////////////////////////////////////////////

//	create a dummy table in case there are no errors.
Script_Log:
load * inline [
ObjectId
];

let vDecimalTime	=	$(vMinutesToGoBack)/1440;
let vNow = num(now(1));
let vLogStart	=	num(now(1)) - $(vDecimalTime);

trace vNow = $(vNow);
trace vLogStart = $(vLogStart);

for each vFile in FileList('$(vFileLocation)\*.txt')

concatenate(Script_Log)
//load if(wildmatch(Text_Log,'Error:*'),1,0) as Err0r_Flag,*;
LOAD '$(vFile)' as File,
	ObjectId & Timestamp as id,
    Sequence#,
    "ProductVersion",
    (num(floor(Timestamp)) +
    	((mid(Timestamp,10,2)*60) +
    	mid(Timestamp,12,2) +
    	(mid(Timestamp,14,2)/100) )
     	/1440) as TimestampNum,
    Timestamp,
    Hostname,
    Id,
    Description,
    ProxySessionId,
    ProxyPackageId,
    RequestSequenceId,
    UserDirectory,
    "UserId",
    ObjectId,
    ObjectName,
    "Service",
    Origin,
    Context,
    Command,
    Result,
    Message,
    Id2
from [$(vFile)]
(txt, utf8, embedded labels, delimiter is '\t', msq)
where Result = 0
    and wildmatch(Command,'Finished task')
	and (num(floor(Timestamp)) +
    	((mid(Timestamp,10,2)*60) +
    	mid(Timestamp,12,2) +
    	(mid(Timestamp,14,2)/100) )
     	/1440) > $(vLogStart);

next vFile;

//exit script;


Succ3ss:
load count(distinct ObjectId) as Succ3ssCount
resident Script_Log;

Let vSucc3ss	=	if(isnull(peek('Succ3ssCount',0,'Succ3ss'))
					,0
                    ,peek('Succ3ssCount',0,'Succ3ss'));
trace vSucc3ss	=	$(vSucc3ss);

drop table Succ3ss;


if $(vSucc3ss)>0 then

let vSuccessTasks = '';

Do while vSucc3ss > 0
    let vSucc3ss = $(vSucc3ss) -1;
    let vTaskName = peek('ObjectName', $(vSucc3ss),'Script_Log');
    // vTaskName save only the task name, without the app name.
    let vTaskName = SubField('$(vTaskName)', '|', 1);


    // Concatenate the name of the task with commas
    let vSuccessTasks = '$(vSuccessTasks)' & '$(vTaskName)';

    // If is the last task, put a ".", else put a "," and the following task name
    if $(vSucc3ss) > 0 then
      let vSuccessTasks = '$(vSuccessTasks)' & ', ';
      else
      let vSuccessTasks = '$(vSuccessTasks)' & '.';
      end if;

    trace vSucc3ss	=	$(vSucc3ss);

Loop

//	some url encoding to eliminate spaces and @
let vRecipient 	= 	replace('$(vRecipient)','@','%40');
let vSubject	=	replace('$(vSubject)',' ','+');
let vFromName	=	replace('$(vFromName)',' ','+');
let	vFromEmail	=	replace('$(vFromEmail)','@','%40');

//let vMessage	= 	'The reload of app <b>' & '$(vFailedTasks)' & '</b> has failed with errors within the last $(vMinutesToGoBack) minutes on the Qlik Sense Server. Log in to the <a href="https://con-qssrv.calprocorp.local/qmc/tasks">QMC</a> to review the tasks and associated log entries.';
let vMessage	= 	'A execucao das tasks <b>' & '$(vSuccessTasks)' & '</b> foram concluidas com sucesso nos ultimos $(vMinutesToGoBack) minutos no servidor $(vServerName).';
let vMessage	=	replace('$(vMessage)',' ','+');

SMTPConnector_SendEmail:
LOAD
    status as SendEmail_status,
    result as SendEmail_result,
    filesattached as SendEmail_filesattached
FROM [$(vQwcConnectionName)]
(URL IS [http://localhost:5555/data?connectorID=SMTPConnector&table=SendEmail&SMTPServer=$(vMailServer)&Port=$(vPort)&SSLmode=$(vSSLMode)&to=$(vRecipient)&subject=$(vSubject)&message=$(vMessage)&html=True&fromName=$(vFromName)&fromEmail=$(vFromEmail)&delayInSeconds=0&appID=&UserName=$(vUserName)&Password=IQNzJnXDviFNwZ9lXENJGg%3d%3d%3anp1PmJsOlJvu3oXtiXV3yg%3d%3d&ignoreProxy=False&asDownload=true], qvx);

trace email sent;
// trace url = 'http://localhost:5555/data?connectorID=SMTPConnector&table=SendEmail&SMTPServer=$(vMailServer)&Port=$(vPort)&SSLmode=$(vSSLMode)&to=$(vRecipient)&subject=$(vSubject)&message=$(vMessage)&html=True&fromName=$(vFromName)&fromEmail=$(vFromEmail)&delayInSeconds=0&appID=&UserName=$(vUserName)&Password=IQNzJnXDviFNwZ9lXENJGg%3d%3d%3anp1PmJsOlJvu3oXtiXV3yg%3d%3d&ignoreProxy=False&asDownload=true';

// IMPORTANT: If, when loading the above script, you receive a 'Script Error' Dialog box with a 'Field Not Found'
// (or other) error, the first thing you should do is copy and paste the full request URL (i.e. the URL between the square [...]
// brackets IN THE QLIKVIEW / QLIK SENSE SCRIPT ERROR DIALOG) into a browser address bar and check the response.

else

trace	No Success Execution found - Not sending email;

end if;
